buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:0.7.2"
    }
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'scalaStyle'
apply plugin: 'distribution'
apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-${config.mod_version}"
group= "com.aergonaut.lifeaquatic" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LifeAquatic"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version

    replaceIn "constants/Constants.scala"
    replace "@@MOD_VERSION@@", config.mod_version
    replace "@@MINECRAFT_VERSION@@", config.minecraft_version
    replace "@@VERSION@@", "${config.minecraft_version}-${config.mod_version}"

    runDir = "eclipse"
}

sourceSets {
    main {
        scala {
            srcDir "src/main/scala"
        }
        java {
            srcDir "vendor/CofHLib/src/main/java"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
}

scalaStyle {
    configLocation = "config/scalaStyle.xml"
    source = "src/main/scala"
}

repositories {
    maven { // the repo from which to get NEI and stuff
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile('mcp.mobius.waila:Waila:1.5.11-RC2-NONEI_1.7.10') {
        exclude group: 'mcp.mobius.waila'
    }
    compile "codechicken:CodeChickenLib:1.7.10-1.1.3.136:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.6.43:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.4.105:dev"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.config.minecraft_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

distributions {
  main {
    contents {
      from(jar.destinationDir) {
        include "*.jar"
      }

      from("./") {
        include "LICENSE"
      }
    }
  }
}

tasks.distTar.compression = Compression.GZIP

task release(dependsOn: ['build', 'distZip', 'distTar'])
